#Область ПрограммныйИнтерфейс

// Получить курсы.
Процедура ПолучитьКурсы() Экспорт
	
	//1. получить прокси по статической ссылке
	WSПрокси = WSСсылки.cbrDailyInfo.СоздатьWSПрокси("http://web.cbr.ru/", "DailyInfo", "DailyInfoSoap");
	
	//2. Получить пакет XDTO, в котором описаны типы объектов
	ПакетXDTO = WSПрокси.ФабрикаXDTO.Пакеты.Получить("http://web.cbr.ru/");
	
	//3. Получить тип объекта XDTO, который используется для задания параметра операции веб-сервиса
	ТипGetCourseOnDate = ПакетXDTO.Получить("GetCursOnDate");
	
	//4. Создать ОбъектXTDO с типом параметра
	ПараметрВебСервиса = WSПрокси.ФабрикаXDTO.Создать(ТипGetCourseOnDate);
	ПараметрВебСервиса.On_date = ДатаЗагрузки;
	
	//5. Вызов метода веб-сервиса
	Результат = WSПрокси.GetCursOnDate(ПараметрВебСервиса);

	Если Результат = Неопределено Тогда

		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не удалось получить курсы валют";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;

	СписокВалют = Результат.GetCursOnDateResult.diffgram.ValuteData.ValuteCursOnDate;

	Курсы.Очистить();

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Валюты.Код КАК Код,
	|	Валюты.СимвольныйКод КАК СимвольныйКод,
	|	Валюты.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Валюты КАК Валюты
	|ГДЕ
	|	НЕ Валюты.ПометкаУдаления";

	РезультатЗапроса = Запрос.Выполнить();

	ТаблицаВалют = РезультатЗапроса.Выгрузить();
	ТаблицаВалют.Индексы.Добавить("Код");

	Для Каждого ОбъектXDTO Из СписокВалют Цикл

		СтрокаКурса = Курсы.Добавить();
		СтрокаКурса.Наименование	= ОбъектXDTO.Vname;
		СтрокаКурса.Код				= ОбъектXDTO.Vcode;
		СтрокаКурса.СимвольныйКод	= ОбъектXDTO.VchCode;
		СтрокаКурса.Курс			= ОбъектXDTO.Vcurs;
		СтрокаКурса.Кратность		= ОбъектXDTO.Vnom;

		НайденнаяСтрока = ТаблицаВалют.Найти(СтрокаКурса.Код, "Код");
		Если НайденнаяСтрока <> Неопределено Тогда

			СтрокаКурса.Валюта = НайденнаяСтрока.Ссылка;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

Процедура ЗаписатьКурсы() Экспорт

	Для Каждого СтрокаКурса Из Курсы Цикл

		Если СтрокаКурса.Валюта.Пустая() Тогда

			Продолжить;
		КонецЕсли;

		Запись 				= РегистрыСведений.КурсыВалют.СоздатьМенеджерЗаписи();
		Запись.Период	 	= ДатаЗагрузки;
		Запись.Валюта	 	= СтрокаКурса.Валюта;
		Запись.Курс		 	= СтрокаКурса.Курс;
		Запись.Кратность 	= СтрокаКурса.Кратность;
		Запись.Пользователь = ПолучениеДанныхТекущегоСеансаСервер.ВернутьЗначениеПараметраСеансаТекущийПользователь();

		Запись.Записать();
	КонецЦикла;

КонецПроцедуры

#КонецОбласти